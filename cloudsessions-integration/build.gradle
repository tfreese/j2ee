plugins {
    id("java")
}

description = "Test for Integration."

configurations {
    cloudsessionsLibs {
        extendsFrom(runtimeOnly)
    }

    cloudsessionsWar {
        canBeConsumed = false
        canBeResolved = true
    }
}

dependencies {
    cloudsessionsLibs(project(":cloudsessions"))
    cloudsessionsWar(project(path: ":cloudsessions", configuration: "cloudsessionsWar"))
}

jar {
    enabled = false
}

test {
    enabled = false
}

tasks.register("copyLibs", Copy) {
    group = "MyTasks"

    dependsOn(":cloudsessions:build")

    delete(layout.buildDirectory)
    into(layout.buildDirectory)

//    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    into("libs") {
        from(configurations.cloudsessionsLibs) {
            exclude("cloudsessions*.jar")

            // Required, if "providedCompile" is used and not "compileOnly" in the war-Projekt.
            // exclude(group: "PROVIDED-DEPENDENCY")
        }
    }

    into("war") {
        from(configurations.cloudsessionsWar)
        rename("cloudsessions-" + project(":cloudsessions").version + ".war", "cloudsessions.war")
    }
}
build.finalizedBy("copyLibs")
